# Maintainer: Richard Hillmann <richie at project0 dot de>
pkgname=project0-sweet-gtk-themes
pkgver=3.0
pkgrel=1
pkgdesc="Sweet themes"
arch=('any')
url="https://github.com/EliverLara/Sweet"
license=('GNU')
conflicts=(
    sweet-gtk-theme
)

_kinds=(Sweet Sweet-mars Sweet-Dark Sweet-Ambar Sweet-Ambar-Blue)
for s in "${_kinds[@]}" ; do
    source+=("${pkgname}-${pkgver}-${s}-v40.zip::https://github.com/EliverLara/Sweet/releases/download/v$pkgver/${s}-v40.zip")
done

sha256sums=('bce82b6ac4807c3c2d14a7a8e5084f1ddfacb2fc63d8a8e10c7a7b17ba2ec94a'
            '7cb76838940756f2c3c155cd880c4f064a35ec09283dd69c9c3ab865cd88560c'
            '28705ec474625ef8643afefa97f56f594134c791d4527806345f10cb86d2a7cd'
            'e5882f106deedbf392b336ecf4b3c02b62795d3eed2c47bba0c628c4a6a0feaa'
            '80f0253a4271059416dab16c765595488c7f388b8376de2fecb376e9a0c2aa57')

prepare(){
    for s in "${_kinds[@]}" ; do
        # remove v40 suffix
        mv "${srcdir}/${s}-v40"  "${srcdir}/${s}"
    done
}

package() {
    themes="${pkgdir}/usr/share/themes"
    for s in "${_kinds[@]}" ; do
        install -dm755 "${themes}/${s}"
        cp -r  "${srcdir}/${s}/"* "${themes}/${s}/"
    done
}
